#Область ПрограммныйИнтерфейс

Процедура ПроверитьНастройкиПартионногоУчета() Экспорт

	НастройкаПартионногоУчета = Константы.НастройкаПартионногоУчета.Получить();
	
	Если Не ЗначениеЗаполнено(НастройкаПартионногоУчета) Тогда 
		Константы.НастройкаПартионногоУчета.Установить(Перечисления.ТипПартионногоУчета.ПоСредней);
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписьюДокументаПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	СуммаДокумента = Источник.Товары.Итог("Сумма");
	
	Если Источник.СуммаДокумента <> СуммаДокумента Тогда
		Источник.СуммаДокумента = СуммаДокумента;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьПерепроведение(НастройкаПартионногоУчетаДо, НастройкаПартионногоУчетаПосле) Экспорт
	
	Если НастройкаПартионногоУчетаДо = НастройкаПартионногоУчетаПосле Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкаПартионногоУчетаДо = Перечисления.ТипПартионногоУчета.LIFO
		И НастройкаПартионногоУчетаПосле = Перечисления.ТипПартионногоУчета.FIFO
		Или НастройкаПартионногоУчетаДо = Перечисления.ТипПартионногоУчета.FIFO
		И НастройкаПартионногоУчетаПосле = Перечисления.ТипПартионногоУчета.LIFO Тогда
		ПерепроводитьПоступления = Ложь;
	Иначе
		ПерепроводитьПоступления = Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеТоваров.Ссылка КАК Ссылка,
		|	ПоступлениеТоваров.МоментВремени КАК МоментВремени
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
		|ГДЕ
		|	ПоступлениеТоваров.Проведен
		|	И &ПерепроводитьПоступления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РеализацияТоваров.Ссылка,
		|	РеализацияТоваров.МоментВремени
		|ИЗ
		|	Документ.РеализацияТоваров КАК РеализацияТоваров
		|ГДЕ
		|	РеализацияТоваров.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	МоментВремени";
	
	Запрос.УстановитьПараметр("ПерепроводитьПоступления", ПерепроводитьПоступления);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НачатьТранзакцию();
	
	Попытка
		
		Пока Выборка.Следующий() Цикл
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		ТекстСообщения = "Проведение успешно выполнено!";
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("ПерепроведениеДокументов", ,,,ОписаниеОшибки);
		ТекстСообщения = СтрШаблон("Не удалось выполнить перепроведение документов по причине: %1", ОписаниеОшибки);
		ОтменитьТранзакцию();
		
	КонецПопытки;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();
	
КонецПроцедуры

#КонецОбласти

